plugins {
    id 'com.palantir.docker' version "0.21.0"
}

task copyAgent(type: Copy) {
    dependsOn ':inspectit-oce-agent:jar'
    from(project(':inspectit-oce-agent').buildDir.toString() + "/inspectit-oce-agent-${version}.jar")
    into(buildDir)
    rename ("inspectit-oce-agent-${version}\\.jar",
            'inspectit-oce-agent.jar')
}

docker {
    name "inspectit/inspectit-oce-agent"
    tags "${version}"
    dockerfile file('agent/Dockerfile')
    files 'agent/entrypoint.sh', "$buildDir/inspectit-oce-agent.jar"
}

task setVersionForDockerCompose(type: Copy) {
    from '.env'
    into buildDir
    filter { line -> line.replaceAll('INSPECTIT_OCE_VERSION=.*', "INSPECTIT_OCE_VERSION=${version}") }
}

task packageDemo(type: Zip) {
    dependsOn 'setVersionForDockerCompose'
    archiveName = "inspectit-oce-demo-${version}.zip"
    destinationDir = file(buildDir)

    from (projectDir){
        exclude "build.gradle"
        exclude "README.md"
        exclude "build/"
        exclude "agent/"
        exclude "**/Dockerfile"
        exclude ".env"
    }

    from (buildDir){
        include ".env"
    }
}


docker.dependsOn copyAgent