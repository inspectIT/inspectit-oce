plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'java'
	id "de.undercouch.download" version "4.0.0"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'de.undercouch.download'

group = 'rocks.inspectit.oce'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

task downloadBoomerangZipFile(type: Download) {
	src "https://github.com/akamai/boomerang/archive/${boomerangVersion}.zip"
	dest new File(buildDir, 'boomerang-source.zip')
	overwrite false
}

task downloadAndUnzipBoomerang(dependsOn: downloadBoomerangZipFile, type: Copy) {
	from zipTree(downloadBoomerangZipFile.dest)
	into project.buildDir
}

bootJar {
	dependsOn downloadAndUnzipBoomerang

	archivesBaseName = 'inspectit-ocelot-eum-server'
	version = "${buildVersion}"

	manifest {
		attributes 'Start-Class': 'rocks.inspectit.oce.eum.server.EUMServerApplication'
	}

	from ("$buildDir/boomerang-${boomerangVersion}"){
		include "plugins/*.js"
		include "boomerang.js"
		into "static/boomerang"
	}
}

dependencies {
	implementation(
			project(':inspectit-ocelot-config'),
			"org.springframework.boot:spring-boot-starter-web",
			"org.springframework.security:spring-security-web:5.1.5.RELEASE",
			"io.opencensus:opencensus-api:${openCensusVersion}",
			"io.opencensus:opencensus-impl:${openCensusVersion}",
			"io.prometheus:simpleclient_httpserver:${prometheusClientVersion}",
			"io.opencensus:opencensus-exporter-stats-prometheus:${openCensusVersion}",
			'com.maxmind.geoip2:geoip2:2.12.0',
			'commons-net:commons-net:3.3',
			'org.springframework.boot:spring-boot-starter-actuator'
	)
	compileOnly "org.projectlombok:lombok:1.18.4"
	annotationProcessor "org.projectlombok:lombok:1.18.4"
	testImplementation(
			project(':inspectit-ocelot-config'),
			"org.springframework.boot:spring-boot-starter-test",
			"io.opencensus:opencensus-impl:${openCensusVersion}",
			'org.springframework:spring-test:5.1.3.RELEASE',
			'org.apache.httpcomponents:httpclient:4.5.6',
			'org.mockito:mockito-core:2.23.4',
			'org.junit.jupiter:junit-jupiter-api:5.3.1',
			'org.mockito:mockito-junit-jupiter:2.23.0'
	)
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}
